// =============================================================================
// UT Demo Manager v3.4
// Originally written by UsAaR33
// Project continued by AnthraX after v3.0
// =============================================================================

// ============================================================
// udemo.DemoReader: Non-native class to be able to point to windows.. and easier for updates.
// ============================================================

class DemoReader expands udnative;
var DemoMainClientWindow Control;
var bool bBlocked; //prevent any package adding!

event PackageRequired (string package, int size, bool Installed, GUID myGUID, int gen, bool Cached){
  if (bBlocked)
    return;
  if (package=="")
    package=" ";
  if (!Installed)
    Cached=false; //just in case...
  Control.Packages.AddPackage(Package,Size,2*byte(Installed)-byte(Cached),myGUID,gen).CheckPBI(Control.PBI);
    //control.GetPlayerowner().ClientMessage(package@"is of size"@size@"and is Installed?"@Installed);
}

event DemoReadDone(string Map, bool bServerDemo, float Time, int NumFrames){
    local int i;
    local DemoList MapItem;
    if (bBlocked)
      return;
    Control.UserWindow.LastDemoItem.MapName=Map;
    Control.UserWindow.LastDemoItem.bServerDemo=bServerDemo;
    Control.UserWindow.LastDemoItem.NumFrames=NumFrames;
    Control.UserWindow.LastDemoItem.PlayTime=Time;
    Control.UserWindow.ShotTime=1;
    //force .unr extension on maps (for more efficient downloading)
    MapItem = Control.Packages.FindPackage(Map);
    i = instr(MapItem.PackageName,".");
    if (i==-1)
      MapItem.PackageName = MapItem.PackageName$".unr";
}
//summarize info:
function bool SaveInfo(){
local UDComboListItem Demo;
local string Save, N;
local DemoList Package, CSHP;

Demo=Control.UserWindow.LastDemoItem;
for (Package=DemoList(control.Packages.Next);Package!=none;Package=DemoList(Package.Next))
  if (Package.bIsCSHP)
    break;
N=CHR(13)$CHR(10); //new line :)
CSHP=Package;
//write stuff here:
Save="Demo summary auto-generated by Unreal Tournament Demo Manager 3.0 BETA"$N$
"Available at http://www.usaar33.com"$N$
N$
Demo.Value$".dem Information:"$N$
"Map name:"@Demo.MapName$".unr"$N$
"Demo Type: ";
if (Demo.bServerDemo)
  Save=Save$"Server";
else
  Save=Save$"Client";
Save=Save$"-Side"$N$
Demo.NumFrames$" frames"$N$
"PlayTime: "$class'DemoSettings'.static.ParseTime(Demo.PlayTime)$" (min:seconds)"$N$
"Avg. FPS: "$Demo.NumFrames/Demo.PlayTime$N$
"Cheat Protection: "$Control.PBI.CSHPVer.GetValue();
if (Control.PBI.CSHPVer.GetValue()!="None")
   Save=Save$".U (cached as '"$GUIDstring(CSHP.PackageGUID)$".uxx')";
Save=Save$N$
N$
"File Requirements:";
if (Control.PBI.UsesBp1)
  Save=Save$N$"-Bonus Pack 1";
if (Control.PBI.UsesBp4)
  Save=Save$N$"-Bonus Pack 4";
if (Control.PBI.UsesRA)
  Save=Save$N$"-Rocket Arena";
for (Package=DemoList(control.Packages.Next);Package!=none;Package=DemoList(Package.Next))
  if (!Package.bPBIShows)
    Save=Save$N$Package.PackageName@"(cached as '"$GUIDstring(Package.PackageGUID)$".uxx') -size: "$Package.PackageSize@"bytes";
return WriteDemoInfo(Demo.Value2$Demo.Value,Save);
}
defaultproperties {
}
